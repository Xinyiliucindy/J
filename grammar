// A grammar for j--, suitable for parsing by recursive descent
// Copyright 2013 Bill Campbell, Swami Iyer and Bahar Akbal-Delibas

compilationUnit ::= [PACKAGE qualifiedIdentifier SEMI]
                    {IMPORT  qualifiedIdentifier SEMI}
                    {typeDeclaration} EOF 

qualifiedIdentifier ::= IDENTIFIER {DOT IDENTIFIER}

typeDeclaration ::= modifiers classDeclaration

modifiers ::= {PUBLIC | PROTECTED | PRIVATE | STATIC | ABSTRACT}

classDeclaration ::= CLASS IDENTIFIER 
                       [EXTENDS qualifiedIdentifier] 
                       classBody

classBody ::= LCURLY {modifiers memberDecl} RCURLY

memberDecl ::= IDENTIFIER            // constructor
                 formalParameters 
                    [ THROWS qualifiedIdentifier { COMMA qualifiedIdentifier } ]
             | (VOID | type) IDENTIFIER  // method
                 formalParameters
                    [ THROWS qualifiedIdentifier { COMMA qualifiedIdentifier } ] ( block | SEMI )(block | SEMI)
             | type variableDeclarators SEMI // field

block ::= LCURLY {blockStatement} RCURLY 

blockStatement ::= localVariableDeclarationStatement
                 | statement

statement ::= block
            | IF parExpression statement [ELSE statement]
            | WHILE parExpression statement
            | FOR LPAREN [forInit] SEMI [expression] SEMI [forUpdate] RPAREN statement // step 3
            | RETURN [expression] SEMI
            | SEMI 
            | tryStatement             //step 2 Try
            | THROW expression SEMI              //step 2 Throw
            | statementExpression SEMI

//step 2 Try
tryStatement ::= TYR Block catchClause {catchClause} [FINALLY Block] | FINALLY Block
catchClause ::= CATCH catchFormalParameter Block
catchFormalParameter ::= LPAREN catchType {BTOR catchType} identifier RPAREN

formalParameters ::= LPAREN 
                       [formalParameter {COMMA formalParameter}]
                     RPAREN

formalParameter ::= type IDENTIFIER

parExpression ::= LPAREN expression RPAREN

forInit ::= statementExpression {COMMA statementExpression} | [final] type variableDeclarators // step 3  : for 

forUpdate ::= statementExpression {COMMA statementExpression} // step 3  : for 

localVariableDeclarationStatement ::= type variableDeclarators SEMI

variableDeclarators ::= variableDeclarator {COMMA variableDeclarator}

variableDeclarator ::= IDENTIFIER [ASSIGN variableInitializer]
                    
variableInitializer ::= arrayInitializer | expression

arrayInitializer ::= LCURLY 
                       [variableInitializer
                         {COMMA variableInitializer} [COMMA]]
                     RCURLY 

arguments ::= LPAREN [expression {COMMA expression}] RPAREN

type ::= referenceType | basicType


// step 1
basicType ::= BOOLEAN | CHAR | INT | BYTE | SHORT | LONG | DOUBLE | FLOAT 

referenceType ::= basicType LBRACK RBRACK {LBRACK RBRACK}
                | qualifiedIdentifier {LBRACK RBRACK}

statementExpression ::= expression // but must have side-effect, eg i++

expression ::= assignmentExpression

// level 13 
assignmentExpression ::= conditionalExpression  // must be a valid lhs
                           [(ASSIGN | PLUS_ASSIGN | MINUS_ASSIGN                            
                            | STAR_ASSIGN | DIV_ASSIGN | REM_ASSIGN                       
                            | SSL_ASSIGN | SSR_ASSIGN | USR_ASSIGN
                            | AND_ASSIGN | BITXOR_ASSIGN | BTOR_ASSIGN) assignmentExpression]

// level 12
conditionalExpression ::= conditionalOrExpression
                          [ QUESTION assignmentExpression COLON conditionalExpression]

//level 11
conditionalOrExpression ::= conditionalAndExpression 
                          { LOR conditionalAndExpression}

// level 10
conditionalAndExpression ::= bitwiseOrExpression
                          { LAND bitwiseOrExpression}

// level 9
// gengxingguang: added level 9
bitwiseOrExpression  ::= bitXorExpression
                            { BTOR bitXorExpression}

// level 8
// yaoyao: added level 8
bitXorExpression ::= andExpression
                          { BITXOR andExpression}

// level 7
// zoe: add level 7
andExpression ::= equalityExpression
                          { AND equalityExpression}

// level 6
equalityExpression ::= relationalExpression  
                         { EQUAL relationalExpression}


// level 5
relationalExpression ::= binaryshiftExpression
                           [(GT | LE) binaryshiftExpression
                           | INSTANCEOF referenceType]

// level 4               
// xinyi: added level 4
binaryshiftExpression ::= additiveExpression
                          {(SSLEFT | SSRIGHT | USRIGHT)  additiveExpression}

// level 3
additiveExpression ::= multiplicativeExpression
                         {(PLUS | MINUS) multiplicativeExpression}

// level 2
multiplicativeExpression ::= unaryExpression
                               {(STAR | DIV | REM) unaryExpression}

// level 1
unaryExpression ::= INC unaryExpression
                  | MINUS unaryExpression
                  | PLUS unaryExpression
                  | COMPLEMENT  unaryExpression
                  | simpleUnaryExpression

simpleUnaryExpression ::= LNOT unaryExpression
                        | LPAREN basicType RPAREN unaryExpression //casted
                        | LPAREN // casted               
                            referenceType
                          RPAREN simpleUnaryExpression
                        | postfixExpression 

postfixExpression ::= primary {selector} {DEC|INC}

selector ::= DOT qualifiedIdentifier [arguments]
           | LBRACK expression RBRACK

primary ::= parExpression
          | THIS [arguments]
          | SUPER ( arguments 
                  | DOT IDENTIFIER [arguments] 
                  )
          | literal
          | NEW creator
          | qualifiedIdentifier [arguments]

creator ::= (basicType | qualifiedIdentifier) 
              ( arguments
              | LBRACK RBRACK {LBRACK RBRACK} [arrayInitializer]
              | newArrayDeclarator
              )

newArrayDeclarator ::= LBRACK expression RBRACK 
                         {LBRACK expression RBRACK} {LBRACK RBRACK}

literal ::= INT_LITERAL | CHAR_LITERAL | STRING_LITERAL
          | TRUE        | FALSE        | NULL


// 3.10  all levels adding finish